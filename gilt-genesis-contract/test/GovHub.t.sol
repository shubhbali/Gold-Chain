pragma solidity ^0.8.10;

import "./utils/Deployer.sol";

contract GovHubTest is Deployer {
    event failReasonWithStr(string message);

    function setUp() public { }

    function testGovValidatorSet(uint16 value) public {
        vm.assume(value >= 100);
        vm.assume(value <= 1e5);

        bytes memory key = "expireTimeSecondGap";
        bytes memory valueBytes = abi.encode(value);
        vm.expectEmit();
        emit failReasonWithStr("unknown param");
        _updateParamByGovHub(key, valueBytes, address(giltValidatorSet));
    }

    function testGovSlash(uint16 value1, uint16 value2) public {
        uint256 misdemeanorThreshold = slashIndicator.misdemeanorThreshold();
        vm.assume(uint256(value1) > misdemeanorThreshold);
        vm.assume(value1 <= 1000);
        vm.assume(value2 < value1);
        vm.assume(value2 > 0);

        bytes memory key = "felonyThreshold";
        bytes memory valueBytes = abi.encode(value1);
        vm.expectEmit(false, false, false, true, address(slashIndicator));
        emit paramChange(string(key), valueBytes);
        _updateParamByGovHub(key, valueBytes, address(slashIndicator));
        assertEq(uint256(value1), slashIndicator.felonyThreshold());

        key = "misdemeanorThreshold";
        valueBytes = abi.encode(value2);
        vm.expectEmit(false, false, false, true, address(slashIndicator));
        emit paramChange(string(key), valueBytes);
        _updateParamByGovHub(key, valueBytes, address(slashIndicator));
        assertEq(uint256(value2), slashIndicator.misdemeanorThreshold());
    }
}
